version = '0.0.1'
group = 'ua.in.smartjava.spring-rest-docs'

buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
        asciiDoctorPluginVersion = '1.5.3'
        asciiDoctorPdf = '1.5.0-alpha.11'
        epubVersion = '1.5.0-alpha.6'
    }
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciiDoctorPluginVersion}")
        classpath("org.asciidoctor:asciidoctorj-epub3:${epubVersion}")
        classpath("org.asciidoctor:asciidoctorj-pdf:${asciiDoctorPdf}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.asciidoctor.convert'

ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    dependsOn test
    backends = ['html5', 'pdf', 'epub3', 'docbook']
    options doctype: 'book'

    attributes = [
            'source-highlighter': 'highlightjs',
            'imagesdir'         : './images',
            'toc'               : 'left',
            'numbered'          : '',
            'icons'             : 'font',
            'setanchors'        : '',
            'idprefix'          : '',
            'idseparator'       : '-',
            'docinfo1'          : '',
            'snippets'          : snippetsDir,
            linkattrs           : true,
            encoding            : 'utf-8'
    ]

    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    sourceDir 'src/docs/asciidoc'
    sources {
        include 'index.adoc', 'internal.adoc'
    }
}

jar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        include '**/index.html'
        include '**/images/image.png'
        into 'static/docs'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile 'com.h2database:h2:1.4.194'
    compile('org.projectlombok:lombok')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile('com.jayway.jsonpath:json-path')
}

task publish(type: GradleBuild) {
//    dependsOn asciidoctor
    buildFile = file('publish.gradle')
    tasks = ['publishGhPages']
}