version = '0.0.1'
group = 'ua.in.smartjava.asciidoctor'

buildscript {
    ext {
        asciiDoctorPluginVersion = '1.5.3'
    }
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciiDoctorPluginVersion}")
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.1.5'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.jruby-gradle.base'

ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    dependsOn jrubyPrepareGems
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepareGems.outputDir
    backends = ['html5']
    options doctype: 'book'

    attributes = [
            'source-highlighter': 'highlightjs',
            'imagesdir'         : './images',
            'toc'               : 'right',
            'icons'             : 'font',
            'setanchors'        : '',
            'idprefix'          : '',
            'idseparator'       : '-',
            'docinfo1'          : '',
            'snippets'          : snippetsDir,
            linkattrs           : true,
            encoding            : 'utf-8'
    ]

    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    sourceDir 'src/docs/asciidoc'
    sources {
        include 'index.adoc'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.2.0'
}